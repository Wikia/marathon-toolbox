apply plugin: 'maven-publish'
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    sourcesJar
    javadocJar
}

publishing {
    repositories {
        maven {
            if (project.ext.has('artifactoryUsername')) {
                credentials {
                    username project.ext.artifactoryUsername
                    password project.ext.artifactoryPassword
                }
            }
            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://artifactory.wikia-inc.com/artifactory/libs-snapshot-local"
            } else {
                url "https://artifactory.wikia-inc.com/artifactory/plugins-source"
            }
        }
    }
    publications {
        library(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}


